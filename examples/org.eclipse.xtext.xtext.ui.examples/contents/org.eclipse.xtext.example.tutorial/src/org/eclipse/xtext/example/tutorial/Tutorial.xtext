/*******************************************************************************
 * Copyright (c) 2012 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.example.tutorial.Tutorial with org.eclipse.xtext.xbase.Xbase

generate tutorial "http://www.eclipse.org/xtext/example/tutorial/Tutorial"

/**
 * Parsing always starts with the first rule in the grammar.
 */
DomainModelFile:
	// we start with an optional package declaration (? == optional)
	('package' name=QualifiedName)?

	// the import section is made up of zero or more import statements (* == 0..n) 
	imports+=Import*

	// followed by any number of Entity declarations
	entities+=Entity*
;

/**
 * The framework automatically treats properties with the name 'importedNamespace' as imports. 
 */
Import:
	'import' importedNamespace=QualifiedName
;

/**
 * Entities may have a super type and they may define properties.
 */
Entity:
	'entity' name=ValidID ('extends' superType=JvmTypeReference)? '{'
		features+=Property*
	'}'
;

/**
 * Properties are basically a pair of a name and a reference to a type.
 */
Property:
	name=ValidID ':' type=JvmTypeReference
; 
